version '0.8.4'

apply plugin: 'java'
apply plugin: 'jacoco'
apply plugin: 'application'

sourceCompatibility = 1.8

repositories {
    mavenCentral()
}

dependencies {
    compile 'gnu.getopt:java-getopt:[1.0.13,)'
    compile 'org.bouncycastle:bcprov-jdk15on:[1.55,)'
    compile 'io.vertx:vertx-core:[3.3.2,)'
    compile group: 'org.apache.logging.log4j', name: 'log4j-api', version: '2.7'
    compile group: 'org.apache.logging.log4j', name: 'log4j-core', version: '2.7'
    testCompile 'junit:junit:4.12'
}

compileJava {
    options.encoding = 'UTF-8'
    options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

mainClassName = 'shadowsocks.Main'

applicationName = 'shadowsocks'

task ('fatJar', type: Jar, dependsOn: classes){
    manifest {
        attributes 'Implementation-Title': 'Shadowsocks-Java',
                   'Implementation-Version': version,
                   'Main-Class': 'shadowsocks.Main'
    }
    baseName = 'shadowsocks'
    appendix = 'fat' + '-' + getGitVersion()
    from {configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }} //pack dependent jar
    from 'build/classes/main'
    from 'build/resources/main'
    exclude 'META-INF/*.RSA', 'META-INF/*.SF','META-INF/*.DSA'
}

jacocoTestReport {
    reports {
        xml.enabled = true
        html.enabled = true
    }
}
check.dependsOn jacocoTestReport

def releaseTime() {
    return new Date().format("yyyy-MM-dd", TimeZone.getTimeZone("UTC"))
}

def getGitVersion() {
    return 'git rev-parse --short HEAD'.execute().text.trim()
}
